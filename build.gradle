plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'java'
    id "com.bmuschko.docker-remote-api" version "4.6.2"    
}

apply plugin: 'io.spring.dependency-management'
apply from: "$rootDir/gradle/versioning.gradle"

group = 'ch.so.agi'

ext {
    apacheCamelVersion = "2.23.1"
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    jcenter()    
    maven { url "http://jars.interlis.ch" } 
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    
    //exclude group: 'org.slf4j', module: 'slf4j-simple'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-actuator'
    implementation 'commons-io:commons-io:2.6'
    implementation "org.apache.camel:camel-spring-boot-starter:${apacheCamelVersion}"
    implementation "org.apache.camel:camel-ftp:${apacheCamelVersion}"
    implementation "org.apache.camel:camel-mail:${apacheCamelVersion}"
    implementation "org.apache.camel:camel-zipfile:${apacheCamelVersion}"
    implementation "org.apache.camel:camel-aws:${apacheCamelVersion}"
    //implementation ('ch.so.agi.camel:camel-ili2pg:latest.integration') {changing = true}
    //implementation ('ch.so.agi.camel:camel-ili2pg:1.0.18')
    implementation "ch.interlis:ili2pg:4.3.1"
    implementation "ch.ehi:ehibasics:1.2.0"
    implementation "ch.so.agi.av:av2ch:1.0.12"
    implementation "ch.interlis:av2geobau:1.0.0-SNAPSHOT"
    //implementation ('ch.so.agi.camel:camel-components:latest.integration') {changing = true}
    
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.1'
    
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

task deleteFilesJarDir(type: Delete) {
    delete fileTree("${buildDir}/dependency/")
    delete file("${buildDir}/dependency/")
}

task unzipJar(type: Copy) {
    from zipTree(file("${buildDir}/libs/avdpool-"+version+".jar"))
    into file("${buildDir}/dependency/")
}
unzipJar.dependsOn(deleteFilesJarDir)
bootJar.finalizedBy(unzipJar)

// Build Docker image
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

docker {
    registryCredentials {
        username = System.env.dockerRegistryUser
        password = System.env.dockerRegistryPass
    }
}

task buildDockerImage(type: DockerBuildImage) {
    description = "Build docker image."
    inputDir = project.projectDir
    tags = ["sogis/avdpool:"+ version, "sogis/avdpool:latest"]
    labels = ["avdpool.created": new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date()), 
            "avdpool.git_commit":  getCheckedOutGitCommitHash(), 
            "avdpool.travis_build": version]
}

task pushDockerImage_One(type: DockerPushImage) {
    description = "Push docker image to hub.docker.com (images are UNTESTED)."
    imageName = buildDockerImage.tags.get().toArray()[0]
}

task pushDockerImage_Two(type: DockerPushImage) {
    description = "Push docker image to hub.docker.com (images are UNTESTED)."
    imageName = buildDockerImage.tags.get().toArray()[1]
}

task pushDockerImages() {
    description = "Collect all push images tasks."
}

pushDockerImages.dependsOn {
    tasks.findAll { task -> task.name.startsWith('pushDockerImage_') }
}

import java.text.SimpleDateFormat

def getCheckedOutGitCommitHash() {
    'git log -1 --pretty=%H'.execute().text.trim()
}

def getTimestamp() {
    def date = new Date()
    return date.format('yyyy-MM-dd HH:mm:ss')
}

// Create a static text file with same meta information as the docker labels
task versionTxt()  {
    outputs.upToDateWhen { false }
    doLast {
        new File("version.txt").text = """
Version: $version
Revision: ${getCheckedOutGitCommitHash()}
Buildtime: ${getTimestamp()}
Application-name: avdpool
"""
    }
}
task copyVersionTxt(type: Copy) {
    from "version.txt"
    into "src/main/resources/static/"
}
copyVersionTxt.dependsOn(versionTxt)
build.dependsOn(copyVersionTxt)
classes.dependsOn(copyVersionTxt)
